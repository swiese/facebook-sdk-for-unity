apply plugin: 'com.android.library'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

def fbSdkVersion = project.hasProperty("sdkVersion") ? sdkVersion : '4.11.0'

repositories {
    // If we are doing a local build use our local dir
    if (project.hasProperty("localRepo")) {
        flatDir {
            dirs localRepo
        }
    } else {
      mavenCentral()
    }
}

dependencies {
    // Use local file instead of maven to enable testing with versions
    // not released to maven.
    if (project.hasProperty("localRepo")) {
        compile 'com.facebook.android:facebook-android-sdk:' + fbSdkVersion + '@aar'
        compile 'com.android.support:support-v4:23.2.1'
        compile 'com.android.support:cardview-v7:23.2.1'
        compile 'com.android.support:customtabs:23.2.1'
        compile 'com.parse.bolts:bolts-android:1.4.0'
    } else {
        compile 'com.facebook.android:facebook-android-sdk:' + fbSdkVersion
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}
